plugins {
    id 'com.android.application'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.github.jpohlmeyer.databuckets"
        minSdk 26
        targetSdk 31
        versionCode 1
        versionName "1.0"

        Properties localProperties = getLocalProperties()
        String dropboxKey = localProperties['DROPBOX_KEY']
        buildConfigField "String", "DROPBOX_KEY", "\"${dropboxKey}\""
        manifestPlaceholders = [dropboxKey: dropboxKey]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
}

dependencies {
    implementation 'com.dropbox.core:dropbox-core-sdk:5.0.0'

    implementation 'com.google.code.gson:gson:2.8.9'

    implementation("org.greenrobot:eventbus:3.3.1")

    // Android
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // General Testing
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.2"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // TODO Delete when https://github.com/google/dagger/issues/3119 is fixed
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.2.41'
    // Dagger Hilt
    implementation 'com.google.dagger:hilt-android:2.40.5'
    annotationProcessor 'com.google.dagger:hilt-compiler:2.40.5'
    // For instrumentation tests
//    androidTestImplementation  'com.google.dagger:hilt-android-testing:2.40.5'
//    androidTestAnnotationProcessor 'com.google.dagger:hilt-compiler:2.40.5'
    // For local unit tests
//    testImplementation 'com.google.dagger:hilt-android-testing:2.40.5'
//    testAnnotationProcessor 'com.google.dagger:hilt-compiler:2.40.5'
}

def getLocalProperties() {
    Properties props = new Properties()
    if (file('../local.properties').exists()) {
        props.load(new FileInputStream(file('../local.properties')))
    }
    return props
}